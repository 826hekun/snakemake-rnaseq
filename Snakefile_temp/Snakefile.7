SAMPLES = ["Cs1_rep1", "Cs1_rep2", "Cs2_rep1", "Cs2_rep2", "Cs3_rep1", "Cs3_rep2", "Cs4_rep1", "Cs4_rep2", "Cs5_rep1", "Cs5_rep2"]

rule all:
  input:
    expand("aligned/{sample}.bam", sample=SAMPLES),
    "quanti/genes.counts.matrix",
    "quanti/genes.TMM.TPM.matrix",
    "quanti/de_result.tsv"

rule trim_fastq:
  input:
    r1="raw_data/{sample}_1.fastq.gz",
    r2="raw_data/{sample}_2.fastq.gz"
  output:
    r1="clean_data/{sample}_1.fastp.fastq.gz",
    r2="clean_data/{sample}_2.fastp.fastq.gz",
    report="clean_data/{sample}.fastp.html",
    json="clean_data/{sample}.fastp.json"
  threads: 4
  log: "logs/{sample}.fastp.log"
  shell:
    """
      fastp -i {input.r1} -I {input.r2} \
        -o {output.r1} -O {output.r2} \
        -h {output.report} -j {output.json} \
        -w {threads} 1> {log} 2>&1
    """

rule build_hisat2_index:
  input:
    genome="ref/genome.fa"
  output:
    idx=expand("genome.hisat2.{idx}.ht2", idx=range(1, 9))
  log: "logs/build_hisat2_index.log"
  shell:
    """
      hisat2-build {input.genome} genome.hisat2 1>{log} 2>&1
    """

rule align_hisat2:
  input:
    r1="clean_data/{sample}_1.fastp.fastq.gz",
    r2="clean_data/{sample}_2.fastp.fastq.gz",
    idx=expand("genome.hisat2.{idx}.ht2", idx=range(1, 9))
  output:
    sam="aligned/{sample}.sam",
    log="aligned/{sample}.hisat2.log"
  threads: 4
  log: "logs/{sample}.hisat2.log"
  shell:
    """
      hisat2 -x genome.hisat2 -1 {input.r1} -2 {input.r2} \
        --new-summary --rna-strandness RF \
        -S {output.sam} \
 	-p {threads} \
        1> {output.log} 2> {log}
    """

rule samtools_sort_idx:
  input:
    sam="aligned/{sample}.sam"
  output:
    bam="aligned/{sample}.bam",
    idx="aligned/{sample}.bam.bai"
  threads: 4
  log: "logs/{sample}.sort.log"
  shell:
    """
      samtools sort -@ {threads} -o {output.bam} {input.sam} 1>{log} 2>&1
      samtools index {output.bam}
    """

rule quanti_featureCounts:
  input:
    bam="aligned/{sample}.bam",
    gtf="ref/genes.gtf"
  output:
    expr="quanti/{sample}.count",
    log="quanti/{sample}.log"
  params:
    prefix="quanti/{sample}"
  shell:
    """
      Rscript software/RunFeatureCounts/run-featurecounts.R \
	-b {input.bam} -g {input.gtf} -s 2 -o {params.prefix}
    """

rule abundance_estimates_to_matrix:
  input:
    exprs=expand("quanti/{sample}.count", sample=SAMPLES)
  output:
    counts_mat="quanti/genes.counts.matrix",
    tpm_mat="quanti/genes.TMM.TPM.matrix"
  log: "logs/abundance_estimates_to_matrix.log"
  shell:
    """
    ls {input.exprs} >quant_files.txt
    perl software/RunFeatureCounts/abundance_estimates_to_matrix.pl \
	--est_method featureCounts \
        --out_prefix quanti/genes \
        --quant_files quant_files.txt 1>{log} 2>&1

    # add feature id
    sed -i '1s/^/gene_id/' {output.counts_mat}
    sed -i '1s/^/gene_id/' {output.tpm_mat}
    """

rule de_analysis:
  input:
    counts_mat="quanti/genes.counts.matrix"
  output:
    de_result="quanti/de_result.tsv"
  log: "logs/de_analysis.log"
  shell:
    """
      perl /home/zhxd2/.conda/envs/RNASnake/opt/trinity-2.1.1/Analysis/DifferentialExpression/run_DE_analysis.pl \
        --matrix {input.counts_mat} --method DESeq2 \
        --samples_file samples.txt --contrasts contrasts.txt \
        --output DE_analysis 1>{log} 2>&1

    # Combine all DE_results files
      awk 'FNR==1 && NR!=1{{next}}{{print}}' DE_analysis/*DE_results > {output.de_result}

    # Remove the diff_expr_results directory
      rm -rf DE_analysis
    """
